--JIRA ID EDW-936

USE EDW2
GO

PRINT 'Patching Fact Flight Metric. Started at: ' + CONVERT(VARCHAR(25), GETDATE(), 120)


IF OBJECT_ID('tempdb..#FFM_SESSION_DEVICE_CNT') IS NOT NULL
    DROP TABLE #FFM_SESSION_DEVICE_CNT

IF OBJECT_ID('tempdb..#FFM_PRODUCT_USAGE_CNT') IS NOT NULL
	DROP TABLE #FFM_PRODUCT_USAGE_CNT

--Fetch USER_SESSION_CNT and DEVICE_CNT
--from FACT_SESSION_METRIC grouping by FLIGHT_ID

SELECT FSM.FLIGHT_ID
,COUNT(*)									  as  USER_SESSION_CNT
,SUM(FSM.DEVICE_CNT)						  as  DEVICE_CNT
,SUM(PAID_USAGE_CNT) + SUM(NONPAID_USAGE_CNT) as  USAGE_CNT 
INTO #FFM_SESSION_DEVICE_CNT
FROM dbo.FACT_SESSION_METRIC FSM WITH (NOLOCK) 
JOIN dbo.FACT_FLIGHT_METRIC  FFM WITH (NOLOCK)
ON FSM.FLIGHT_ID = FFM.FLIGHT_ID 
WHERE FSM.FLIGHT_ID > 0
GROUP BY FSM.FLIGHT_ID


--Update USER_SESSION_CNT, DEVICE_CNT and USAGE_CNT
--in FACT_FLIGHT_METRIC 

UPDATE FFM
SET  FFM.USER_SESSION_CNT =		SDC.USER_SESSION_CNT
    ,FFM.DEVICE_CNT		  =		SDC.DEVICE_CNT
	,FFM.USAGE_CNT		  =		SDC.USAGE_CNT  
FROM dbo.FACT_FLIGHT_METRIC FFM
JOIN #FFM_SESSION_DEVICE_CNT SDC
ON FFM.FLIGHT_ID = SDC.FLIGHT_ID 

PRINT 'USER_SESSION_CNT, DEVICE_CNT & USAGE_CNT in FACT_FLIGHT_METRIC are updated successfully for records: ' + convert(varchar(20),@@ROWCOUNT)

--Fetch PRODUCT_USAGE_CNT from FACT_USAGE_PRODUCT_TRANSACTION

SELECT FUP.FLIGHT_ID, 
COUNT(DISTINCT Product_ID) as PRODUCT_USAGE_CNT 
INTO #FFM_PRODUCT_USAGE_CNT
FROM dbo.FACT_USAGE_PRODUCT_TRANSACTION FUP WITH (NOLOCK)
JOIN dbo.FACT_FLIGHT_METRIC FFM WITH (NOLOCK)
ON FUP.FLIGHT_ID = FFM.FLIGHT_ID 
GROUP BY FUP.FLIGHT_ID

--update PRODUCT_USAGE_CNT in FACT_FLIGHT_METRIC

UPDATE FFM
SET FFM.PRODUCT_USAGE_CNT = PUC.PRODUCT_USAGE_CNT
FROM dbo.FACT_FLIGHT_METRIC FFM
JOIN #FFM_PRODUCT_USAGE_CNT PUC
ON FFM.FLIGHT_ID = PUC.FLIGHT_ID 

PRINT 'PRODUCT_USAGE_CNT in FACT_FLIGHT_METRIC is updated successfully for records: ' + convert(varchar(20),@@ROWCOUNT)

IF OBJECT_ID('tempdb..#FFM_SESSION_DEVICE_CNT') IS NOT NULL
    DROP TABLE #FFM_SESSION_DEVICE_CNT

IF OBJECT_ID('tempdb..#FFM_PRODUCT_USAGE_CNT') IS NOT NULL
	DROP TABLE #FFM_PRODUCT_USAGE_CNT

PRINT 'Patching Fact Flight Metric. Finished at: ' + CONVERT(VARCHAR(25), GETDATE(), 120)
GO
